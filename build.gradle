subprojects {
  apply from: "../gradle/dependencies.gradle"

  buildscript {
    repositories {
      google()
      jcenter()
    }

    dependencies {
      classpath dep.androidPlugin
      classpath dep.gradleNexus
    }
  }
  repositories {
    google()
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME

  tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
  }

  if (!(project.name.equals('rxgroups-processor')
          || project.name.equals('rxgroups-annotation-test'))) {
    apply plugin: 'checkstyle'

    checkstyle {
      toolVersion = '7.6.1'
      configFile rootProject.file('checkstyle.xml')
      configProperties = ['checkstyle.cache.file': rootProject.file('build/checkstyle.cache')]
      showViolations true
    }

    task checkstyle(type: Checkstyle) {
      source 'src/main/java'
      include '**/*.java'

      classpath = files()
    }

    afterEvaluate {
      if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
      }
    }
  }
}
