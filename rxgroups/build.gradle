apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'com.bmuschko.nexus'

checkstyle {
  configFile rootProject.file('checkstyle.xml')
  showViolations true
  configProperties = ['checkstyle.cache.file': rootProject.file('build/checkstyle.cache')]
}

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

project.group = GROUP
project.version = VERSION_NAME

dependencies {
  compile 'io.reactivex:rxjava:1.2.0'
  compile 'com.google.code.findbugs:jsr305:3.0.0'

  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:1.7.0'
}

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}

modifyPom {
  project {
    name 'RxGroups library'
    description POM_DESCRIPTION
    url POM_SCM_URL
    inceptionYear '2016'
    version VERSION_NAME
    groupId GROUP
    artifactId 'rxgroups'

    scm {
      url POM_SCM_URL
      connection POM_SCM_CONNECTION
      developerConnection POM_SCM_DEV_CONNECTION
    }

    licenses {
      license {
        name POM_LICENSE_NAME
        url POM_LICENSE_URL
        distribution POM_LICENSE_DIST
      }
    }

    developers {
      developer {
        id POM_DEVELOPER_ID
        name POM_DEVELOPER_NAME
        email 'android@airbnb.com'
      }
    }
  }
}

extraArchive {
  sources = true
  tests = false
  javadoc = true
}

nexus {
  sign = !isCi()
  repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
  snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}
